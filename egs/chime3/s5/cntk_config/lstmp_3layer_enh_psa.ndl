load=ndlMacroDefine
run=ndlCreateNetwork_LSTMP

ndlMacroDefine=[

]

ndlCreateNetwork_LSTMP=[
    #define basic i/o
    baseFeatDim=$baseFeatDim$
    RowSliceStart=$RowSliceStart$ 
    FeatDim=$featDim$
    stftDim=$stftDim$
    hstftDim=$hstftDim$
    cellDim=$cellDim$
    hiddenDim=$hiddenDim$

    features=Input(FeatDim, tag=feature)
    scalespec=constant(0.00001)
    One=constant(1,rows=1,cols=1)
    iscalespec=constant(100000)
    stftn=InputValue(stftDim, tag=feature)
    stftc=InputValue(stftDim, tag=feature)

    stftnMag = Scale(scalespec,RowSlice(0, hstftDim, stftn));
    stftnPhase = RowSlice(hstftDim, hstftDim, stftn);

    stftcMag = Scale(scalespec,RowSlice(0, hstftDim, stftc));
    stftcPhase = RowSlice(hstftDim, hstftDim, stftc);
			       			       
    feashift=RowSlice(RowSliceStart, baseFeatDim, features);      # shift 5 frames right (x_{t+5} -> x_{t} )


    featNorm = MeanVarNorm(feashift)


    # layer 1
    LSTMoutput1 = LSTMPComponent(baseFeatDim, hiddenDim, cellDim, featNorm);
    # layer 2 
    LSTMoutput2 = LSTMPComponent(hiddenDim, hiddenDim, cellDim, LSTMoutput1);
    # layer 3 
    LSTMoutput3 = LSTMPComponent(hiddenDim, hiddenDim, cellDim, LSTMoutput2);

    W = Parameter(hstftDim, hiddenDim, init=uniform, initValueScale=1);
    b = Parameter(hstftDim, init=fixedvalue, value=0);
    Mask = Sigmoid(Plus(Times(W, LSTMoutput3), b));
	
    MaskedNoisy = ElementTimes(Mask, stftnMag)

    PhaseDifference = Minus(stftnPhase, stftcPhase)
    CosPhaseDifference = Cos(PhaseDifference)
    CleanPSATarget = ElementTimes(stftcMag, CosPhaseDifference)
    
    SmaskedNoisy = Scale(iscalespec, MaskedNoisy)
    MaskedNoisywithPhase = RowStack(SmaskedNoisy, stftnPhase, tag=output)
    
    IdealPSAMask = ElementDivide(CleanPSATarget,stftnMag)
    IdealPSAMaskLim = MinSc(One,IdealPSAMask)
    IdealPSAMaskLimNoisy = ElementTimes(IdealPSAMaskLim, stftnMag)
    
    MSE = SquareError(MaskedNoisy, CleanPSATarget, tag=criteria)
    MaskSum = SumElements(Mask, tag=eval)
    # for debugging only
    NoisyMSE = SquareError(stftnMag, CleanPSATarget, tag=eval)
    BestMSE = SquareError(IdealPSAMaskLimNoisy, CleanPSATarget, tag=eval)
    #CleanSum = SumElements(stftcMag, tag=eval)
    #NoisySum = SumElements(stftnMag, tag=eval)
]
